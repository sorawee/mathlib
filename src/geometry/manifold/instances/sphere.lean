/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import geometry.manifold.charted_space
import analysis.normed_space.inner_product


/-!
# Manifold structure on the sphere

This file defines stereographic projection from the sphere in an inner product space `E`, and uses
it to put a smooth manifold structure on the sphere.

-/

noncomputable theory

open metric

namespace inner_product_space
/-! Lemmas for `analysis.normed_space.inner_product`. -/

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
variables {E : Type*} [inner_product_space ğ•œ E]

open is_R_or_C

notation K`â€ `:1000 := submodule.orthogonal K

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y

/-- The orthogonal projection is the unique point in `K` with the orthogonality property, variant
characterization in terms of the orthogonal complement. -/
lemma eq_orthogonal_projection_of_mem_of_inner_eq_zero' {K : submodule ğ•œ E} [complete_space K]
  {u v : E} (hv : v âˆˆ K) (hvo : u - v âˆˆ Kâ€ ) :
  v = orthogonal_projection K u :=
begin
  apply eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero hv,
  intros w hw,
  rw inner_eq_zero_sym,
  exact hvo w hw
end

lemma eq_proj_of_split (K : submodule ğ•œ E) [complete_space K]
  {v y z : E} (hy : y âˆˆ K) (hz : z âˆˆ Kâ€ ) (hv : y + z = v) :
  y = orthogonal_projection K v :=
begin
  apply eq_orthogonal_projection_of_mem_of_inner_eq_zero' hy,
  convert hz,
  rw â† hv,
  abel
end

lemma eq_proj_of_split' [complete_space E] (K : submodule ğ•œ E) [complete_space K]
  {v y z : E} (hy : y âˆˆ K) (hz : z âˆˆ Kâ€ ) (hv : y + z = v) :
  z = orthogonal_projection (Kâ€ ) v  :=
begin
  suffices hy' : y âˆˆ Kâ€ â€ , -- : set E), v = y' + z,
  { rw add_comm at hv,
    exact eq_proj_of_split (Kâ€ ) hz hy' hv },
  simp [hy]
end

lemma sum_proj' [complete_space E] (K : submodule ğ•œ E) [complete_space K] (w : E) :
  â†‘(orthogonal_projection K w) + â†‘(orthogonal_projection (Kâ€ ) w) = w :=
begin
  obtain âŸ¨y, hy, z, hz, hwyzâŸ© := K.exists_sum_mem_mem_orthogonal w,
  convert hwyz,
  { rw eq_proj_of_split K hy hz hwyz },
  { rw eq_proj_of_split' K hy hz hwyz }
end


lemma sum_proj [complete_space E] (K : submodule ğ•œ E) [complete_space K] :
  K.subtype_continuous.comp (orthogonal_projection K)
  + K.orthogonal.subtype_continuous.comp (orthogonal_projection (Kâ€ ))
  = continuous_linear_map.id ğ•œ E :=
by { ext w, exact sum_proj' K w }

include ğ•œ
/-- Pythagorean theorem, vector inner product form. -/
lemma norm_add_square_eq_norm_square_add_norm_square_of_inner_eq_zero' (x y : E) (h : âŸªx, yâŸ« = 0) :
  âˆ¥x + yâˆ¥ ^ 2 = âˆ¥xâˆ¥ ^ 2 + âˆ¥yâˆ¥ ^ 2 :=
begin
  convert norm_add_square_eq_norm_square_add_norm_square_of_inner_eq_zero _ _ h;
  simp [pow_two]
end

lemma norm_sub_crossmul (v x : E) :
  âˆ¥(âˆ¥vâˆ¥:ğ•œ) â€¢ x - (âˆ¥xâˆ¥:ğ•œ) â€¢ vâˆ¥ * âˆ¥(âˆ¥vâˆ¥:ğ•œ) â€¢ x - (âˆ¥xâˆ¥:ğ•œ) â€¢ vâˆ¥ = 2 * âˆ¥xâˆ¥ * âˆ¥vâˆ¥ * (âˆ¥xâˆ¥ * âˆ¥vâˆ¥ - re âŸªx, vâŸ«) :=
begin
  rw norm_sub_mul_self,
  simp [inner_smul_left, inner_smul_right, norm_smul, is_R_or_C.norm_eq_abs],
  ring
end

lemma norm_sub_crossmul' (v x : E) :
  âˆ¥(âˆ¥vâˆ¥:ğ•œ) â€¢ x - (âˆ¥xâˆ¥:ğ•œ) â€¢ vâˆ¥ ^ 2 = 2 * âˆ¥xâˆ¥ * âˆ¥vâˆ¥ * (âˆ¥xâˆ¥ * âˆ¥vâˆ¥ - re âŸªx, vâŸ«) :=
by { convert norm_sub_crossmul v x, exact pow_two _ }

lemma inner_eq_norm_mul_iff {v x : E}:
  âŸªv, xâŸ« = (âˆ¥xâˆ¥ : ğ•œ) * âˆ¥vâˆ¥ â†” (âˆ¥xâˆ¥ : ğ•œ) â€¢ v = (âˆ¥vâˆ¥ : ğ•œ) â€¢ x :=
begin
  transitivity âˆ¥(âˆ¥xâˆ¥ : ğ•œ) â€¢ v - (âˆ¥vâˆ¥ : ğ•œ) â€¢ xâˆ¥ ^ 2 = 0,
  { rw norm_sub_crossmul' x v,
    split,
    { intros hxv,
      rw hxv,
      simp only [mul_re, norm_eq_zero, of_real_re, sub_zero, mul_zero, of_real_im],
      ring },
    { simp [is_R_or_C.two_ne_zero],
      rintros ((h | h )| h),
      { simp [h] },
      { simp [h] },
      have : abs âŸªv, xâŸ« â‰¤ re âŸªv, xâŸ«,
      { have := @abs_inner_le_norm ğ•œ _ _ _ v x,
        linarith },
      rw â† re_eq_self_of_le this,
      norm_cast,
      linarith } },
  { split,
    { intros h,
      apply eq_of_norm_sub_eq_zero,
      apply pow_eq_zero h },
    { intros h,
      simp [h] } }
end

lemma inner_eq_norm_mul_iff_of_mem_sphere {v x : E} (hv : âˆ¥vâˆ¥ = 1) (hx : âˆ¥xâˆ¥ = 1) :
  âŸªv, xâŸ« = 1 â†” x = v :=
begin
  transitivity v = x,
  { convert inner_eq_norm_mul_iff using 2;
    simp [hv, hx] },
  exact eq_comm
end

end inner_product_space


namespace inner_product_space
/-! Reals-specific lemmas for `analysis.normed_space.inner_product`. -/

variables {E : Type*} [inner_product_space â„ E]

lemma mem_sphere (v w : E) (r : â„) : w âˆˆ sphere v r â†” âˆ¥w - vâˆ¥ = r :=
by simp [dist_eq_norm]

lemma mem_sphere_zero {w : E} {r : â„} : w âˆˆ sphere (0:E) r â†” âˆ¥wâˆ¥ = r :=
by simp [dist_eq_norm]

@[simp] lemma norm_of_mem_unit_sphere (x : sphere (0:E) 1) : âˆ¥(x:E)âˆ¥ = 1 :=
inner_product_space.mem_sphere_zero.mp x.2

lemma inner_eq_norm_mul_iff_real (v x : E) :
  âŸªv, xâŸ«_â„ = âˆ¥xâˆ¥ * âˆ¥vâˆ¥ â†” âˆ¥xâˆ¥ â€¢ v = âˆ¥vâˆ¥ â€¢ x :=
inner_eq_norm_mul_iff

lemma inner_ne_norm_mul_iff_real (v x : E) :
  âŸªv, xâŸ«_â„ < âˆ¥xâˆ¥ * âˆ¥vâˆ¥ â†” âˆ¥xâˆ¥ â€¢ v â‰  âˆ¥vâˆ¥ â€¢ x :=
begin
  have : _ â†” (_ â‰  _):= not_congr (inner_eq_norm_mul_iff_real v x),
  rw â† this,
  refine âŸ¨ne_of_lt, lt_of_le_of_ne _âŸ©,
  rw mul_comm,
  refine le_trans _ (abs_real_inner_le_norm v x),
  exact le_abs_self _,
end

lemma inner_lt_one_iff_of_mem_sphere {v x : E} (hv : âˆ¥vâˆ¥ = 1) (hx : âˆ¥xâˆ¥ = 1) :
  âŸªv, xâŸ«_â„ < 1 â†” x â‰  v :=
begin
  transitivity v â‰  x,
  { convert inner_ne_norm_mul_iff_real v x;
    simp [hv, hx] },
  exact ne_comm
end


end inner_product_space

namespace inner_product_space
/-! Another batch of lemmas for `analysis.normed_space.inner_product`, these ones specific to
projections onto singletons -/

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
variables {E : Type*} [inner_product_space ğ•œ E]

open submodule

notation ğ•œ`âˆ™`:1000 x := span ğ•œ (@singleton _ _ set.has_singleton x)

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y

lemma orthogonal_projection_singleton {v : E} (hv : v â‰  0) (w : E) :
  (âŸªv, wâŸ« / âˆ¥vâˆ¥ ^ 2) â€¢ v = orthogonal_projection (ğ•œ âˆ™ v) w :=
begin
  apply eq_orthogonal_projection_of_mem_of_inner_eq_zero,
  { rw mem_span_singleton,
    use âŸªv, wâŸ« / âˆ¥vâˆ¥ ^ 2 },
  intros x hx,
  rw mem_span_singleton at hx,
  obtain âŸ¨c, rflâŸ© := hx,
  have hv' : â†‘âˆ¥vâˆ¥ ^ 2 = âŸªv, vâŸ« := by { norm_cast, simp [norm_sq_eq_inner] },
  have hv'' : âŸªv, vâŸ« â‰  0 := hv âˆ˜ inner_self_eq_zero.mp,
  have h_div := div_mul_cancel _ hv'',
  simp [inner_sub_left, inner_smul_left, inner_smul_right, is_R_or_C.conj_div, conj_sym, hv'],
  right,
  rw h_div,
  simp [sub_self],
end

lemma orthogonal_projection_unit_singleton {v : E} (hv : âˆ¥vâˆ¥ = 1) (w : E) :
  âŸªv, wâŸ« â€¢ v = orthogonal_projection (ğ•œ âˆ™ v) w :=
begin
  have hv' : v â‰  0,
  { intros h,
    rw â† norm_eq_zero at h,
    rw hv at h,
    norm_num at h },
  convert orthogonal_projection_singleton hv' w,
  rw hv,
  simp
end

lemma prod_zero_left (v : E) {w : E} (hw : w âˆˆ (ğ•œ âˆ™ v)â€ ) : âŸªw, vâŸ« = 0 :=
inner_left_of_mem_orthogonal (mem_span_singleton_self v) hw

lemma prod_zero_right (v : E) {w : E} (hw : w âˆˆ (ğ•œ âˆ™ v)â€ ) : âŸªv, wâŸ« = 0 :=
inner_right_of_mem_orthogonal (mem_span_singleton_self v) hw

lemma proj_orthogonal_singleton [complete_space E] (v : E) :
  orthogonal_projection ((ğ•œ âˆ™ v)â€ ) v = 0 :=
begin
  ext,
  refine (eq_orthogonal_projection_of_mem_of_inner_eq_zero' _ _).symm;
  { simp [mem_span_singleton_self] }
end

end inner_product_space


variables {E : Type*} [inner_product_space â„ E]
variables (v : E)

open inner_product_space submodule

/-- Stereographic projection, forward direction. This is a map from an inner product space `E` to
the orthogonal complement of an element `v` of `E`. It is smooth away from the affine hyperplane
through `v` parallel to the orthogonal complement.  It restricts on the sphere to the stereographic
projection. -/
def stereo_to_fun [complete_space E] (x : E) : (â„ âˆ™ v)â€  :=
(2 / ((1:â„) - inner_left v x)) â€¢ orthogonal_projection ((â„ âˆ™ v)â€ ) x

variables {v}

@[simp] lemma stereo_to_fun_apply [complete_space E] (x : E) :
  stereo_to_fun v x = (2 / ((1:â„) - inner_left v x)) â€¢ orthogonal_projection ((â„ âˆ™ v)â€ ) x :=
rfl

lemma continuous_on_stereo_to_fun [complete_space E] :
  continuous_on (stereo_to_fun v) {x : E | inner_left v x â‰  (1:â„)} :=
begin
  refine continuous_on.smul _ (orthogonal_projection ((â„ âˆ™ v)â€ )).continuous.continuous_on,
  refine continuous_const.continuous_on.div _ _,
  { exact (continuous_const.sub (inner_left v).continuous).continuous_on },
  { intros x h h',
    exact h (sub_eq_zero.mp h').symm }
end

variables (v)

def stereo_inv_fun_aux (w : E) : E := (âˆ¥wâˆ¥ ^ 2 + 4)â»Â¹ â€¢ ((4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ v)

variables {v}

@[simp] lemma stereo_inv_fun_aux_apply (w : E) :
  stereo_inv_fun_aux v w = (âˆ¥wâˆ¥ ^ 2 + 4)â»Â¹ â€¢ ((4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ v) :=
rfl

lemma stereo_inv_fun_aux_mem (hv : âˆ¥vâˆ¥ = 1) {w : E} (hw : w âˆˆ (â„ âˆ™ v)â€ ) :
  stereo_inv_fun_aux v w âˆˆ (sphere (0:E) 1) :=
begin
  rw inner_product_space.mem_sphere_zero,
  have hâ‚ : 0 â‰¤ âˆ¥wâˆ¥ ^ 2 + 4 := by nlinarith,
  suffices : âˆ¥(4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ vâˆ¥ = âˆ¥wâˆ¥ ^ 2 + 4,
  { have hâ‚‚ : âˆ¥wâˆ¥ ^ 2 + 4 â‰  0 := ne_of_gt (by nlinarith),
    simp only [norm_smul, real.norm_eq_abs, abs_inv, this, abs_of_nonneg hâ‚,
      stereo_inv_fun_aux_apply],
    field_simp },
  suffices : âˆ¥(4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ vâˆ¥ ^ 2 = (âˆ¥wâˆ¥ ^ 2 + 4) ^ 2,
  { have hâ‚ƒ : 0 â‰¤ âˆ¥stereo_inv_fun_aux v wâˆ¥ := norm_nonneg _,
    simpa [hâ‚, hâ‚ƒ, -one_pow] using this },
  simp [norm_add_pow_two_real, norm_smul, inner_smul_left, inner_smul_right, prod_zero_left _ hw,
    mul_pow, real.norm_eq_abs, hv],
  ring,
end

/-- Stereographic projection, reverse direction.  This is a map from the orthogonal complement of a
unit vector `v` in an inner product space `E` to the unit sphere in `E`. -/
def stereo_inv_fun (hv : âˆ¥vâˆ¥ = 1) (w : (â„ âˆ™ v)â€ ) : sphere (0:E) 1 :=
âŸ¨stereo_inv_fun_aux v (w:E), stereo_inv_fun_aux_mem hv w.2âŸ©

@[simp] lemma stereo_inv_fun_apply (hv : âˆ¥vâˆ¥ = 1) (w : (â„ âˆ™ v)â€ ) :
  (stereo_inv_fun hv w : E) = (âˆ¥wâˆ¥ ^ 2 + 4)â»Â¹ â€¢ ((4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ v) :=
rfl

lemma stereo_inv_fun_ne_north_pole (hv : âˆ¥vâˆ¥ = 1) (w : (â„ âˆ™ v)â€ ) :
  stereo_inv_fun hv w â‰  (âŸ¨v, by simp [hv]âŸ© : sphere (0:E) 1) :=
begin
  refine subtype.ne_of_val_ne _,
  rw â† inner_lt_one_iff_of_mem_sphere hv,
  { have hw : âŸªv, wâŸ«_â„ = 0 := prod_zero_right v w.2,
    have hw' : (âˆ¥(w:E)âˆ¥ ^ 2 + 4)â»Â¹ * (âˆ¥(w:E)âˆ¥ ^ 2 - 4) < 1,
    { refine (inv_mul_lt_iff' _).mpr _,
      { nlinarith },
      linarith },
    simpa [inner_add_right, inner_smul_right, real_inner_self_eq_norm_square, hw, hv] using hw' },
  { simpa using stereo_inv_fun_aux_mem hv w.2 }
end

lemma continuous_stereo_inv_fun (hv : âˆ¥vâˆ¥ = 1) :
  continuous (stereo_inv_fun hv) :=
begin
  let c : sphere (0:E) 1 â†’ E := coe,
  suffices : continuous (c âˆ˜ (stereo_inv_fun hv)),
  { exact continuous_induced_rng this },
  have hâ‚€ : continuous (Î» w : E, âˆ¥wâˆ¥ ^ 2) := (continuous_pow 2).comp continuous_norm,
  have hâ‚ : continuous (Î» w : E, (âˆ¥wâˆ¥ ^ 2 + 4)â»Â¹),
  { refine (hâ‚€.add continuous_const).inv' _,
    intros w,
    refine ne_of_gt _,
    nlinarith },
  have hâ‚‚ : continuous (Î» w, (4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ v),
  { refine (continuous_const.smul continuous_id).add _,
    refine (hâ‚€.sub continuous_const).smul continuous_const },
  convert (hâ‚.smul hâ‚‚).comp continuous_subtype_coe
end

variables [complete_space E]

lemma stereo_left_inv (hv : âˆ¥vâˆ¥ = 1) {x : sphere (0:E) 1} (hx : (x:E) â‰  v) :
  stereo_inv_fun hv (stereo_to_fun v x) = x :=
begin
  ext,
  simp only [stereo_to_fun_apply, stereo_inv_fun_apply, smul_add],
  -- name two frequently-occuring quantities and write down their basic properties
  set a : â„ := inner_left v x,
  set y := orthogonal_projection ((â„ âˆ™ v)â€ ) x,
  have split : â†‘x = a â€¢ v + â†‘y,
  { convert (sum_proj' (â„ âˆ™ v) x).symm,
    simp [â† orthogonal_projection_unit_singleton hv x, a] },
  have hvy : âŸªv, yâŸ«_â„ = 0 := prod_zero_right v y.2,
  have pythag : 1 = a ^ 2 + âˆ¥(y:E)âˆ¥ ^ 2,
  { have hvy' : âŸªa â€¢ v, yâŸ«_â„ = 0 := by simp [inner_smul_left, hvy],
    convert norm_add_square_eq_norm_square_add_norm_square_of_inner_eq_zero' _ _ hvy' using 2,
    { simp [â† split] },
    { simp [norm_smul, hv, real.norm_eq_abs, abs_sq_eq] } },
  -- two facts which will be helpful for clearing denominators in the main calculation
  have ha : 1 - a â‰  0,
  { have : a < 1 := (inner_lt_one_iff_of_mem_sphere hv (by simp)).mpr hx,
    linarith },
  have : 2 ^ 2 * âˆ¥(y:E)âˆ¥ ^ 2 + 4 * (1 - a) ^ 2 â‰  0,
  { refine ne_of_gt _,
    have := norm_nonneg (y:E),
    have : 0 < (1 - a) ^ 2 := pow_two_pos_of_ne_zero (1 - a) ha,
    nlinarith },
  -- the core of the problem is these two algebraic identities:
  have hâ‚ : (2 ^ 2 / (1 - a) ^ 2 * âˆ¥yâˆ¥ ^ 2 + 4)â»Â¹ * 4 * (2 / (1 - a)) = 1,
  { field_simp,
    nlinarith },
  have hâ‚‚ : (2 ^ 2 / (1 - a) ^ 2 * âˆ¥(y:E)âˆ¥ ^ 2 + 4)â»Â¹ * (2 ^ 2 / (1 - a) ^ 2 * âˆ¥(y:E)âˆ¥ ^ 2 - 4) = a,
  { field_simp,
    transitivity (1 - a) ^ 2 * (a * (2 ^ 2 * âˆ¥(y:E)âˆ¥ ^ 2 + 4 * (1 - a) ^ 2)),
    { congr,
      nlinarith },
    nlinarith },
  -- deduce the result
  convert congr_arg2 has_add.add (congr_arg (Î» t, t â€¢ (y:E)) hâ‚) (congr_arg (Î» t, t â€¢ v) hâ‚‚) using 1,
  { simp [inner_add_right, inner_smul_right, hvy, real_inner_self_eq_norm_square, hv, mul_smul,
      mul_pow, real.norm_eq_abs, abs_sq_eq, norm_smul] },
  { simp [split, add_comm] }
end


lemma stereo_right_inv (hv : âˆ¥vâˆ¥ = 1) (w : (â„ âˆ™ v)â€ ) :
  (stereo_to_fun v âˆ˜ coe) (stereo_inv_fun hv w) = w :=
begin
  have : 2 / (1 - (âˆ¥(w:E)âˆ¥ ^ 2 + 4)â»Â¹ * (âˆ¥(w:E)âˆ¥ ^ 2 - 4)) * (âˆ¥(w:E)âˆ¥ ^ 2 + 4)â»Â¹ * 4 = 1,
  { have : âˆ¥(w:E)âˆ¥ ^ 2 + 4 â‰  0 := by nlinarith,
    field_simp,
    ring },
  convert congr_arg (Î» c, c â€¢ w) this,
  { have hâ‚ : orthogonal_projection ((â„ âˆ™ v)â€ ) v = 0 := proj_orthogonal_singleton v,
    have hâ‚‚ : orthogonal_projection ((â„ âˆ™ v)â€ ) w = w,
    { ext,
      exact orthogonal_projection_mem_subspace_eq_self w.2 },
    have hâ‚ƒ : inner_left v w = (0:â„) := prod_zero_right v w.2,
    have hâ‚„ : inner_left v v = (1:â„) := by simp [real_inner_self_eq_norm_square, hv],
    simp [hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, continuous_linear_map.map_add, continuous_linear_map.map_smul,
      mul_smul] },
  { simp }
end

/-- Stereographic projection from the unit sphere in `E`, centred at a unit vector `v` in `E`; this
is the version as a local homeomorphism. -/
def stereographic (hv : âˆ¥vâˆ¥ = 1) : local_homeomorph (sphere (0:E) 1) ((â„ âˆ™ v)â€ ) :=
{ to_fun := (stereo_to_fun v) âˆ˜ coe,
  inv_fun := stereo_inv_fun hv,
  source := {âŸ¨v, by simp [hv]âŸ©}á¶œ,
  target := set.univ,
  map_source' := by simp,
  map_target' := Î» w _, stereo_inv_fun_ne_north_pole hv w,
  left_inv' := Î» _ hx, stereo_left_inv hv (Î» h, hx (subtype.ext h)),
  right_inv' := Î» w _, stereo_right_inv hv w,
  open_source := is_open_compl_singleton,
  open_target := is_open_univ,
  continuous_to_fun := continuous_on_stereo_to_fun.comp continuous_subtype_coe.continuous_on
    (Î» w h, h âˆ˜ subtype.ext âˆ˜ (inner_eq_norm_mul_iff_of_mem_sphere hv (by simp)).mp),
  continuous_inv_fun :=  (continuous_stereo_inv_fun hv).continuous_on }
